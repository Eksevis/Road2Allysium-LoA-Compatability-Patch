[monster_rollover]
{
  b interface = true;
	centered_x = background;
	intended_resolution_width = 640;
	intended_resolution_height = 480;


	skrit =
	[[
		owner = UIWindow;
//		string skrit_name$ = "monster_rollover.gas";


		show$
		{
			goid goid$ = MakeGoid(GameAuditor.GetDb.GetInt("rollover_goid"));
			if (goid$ == null) { return; }
			if (!goid$.IsValid()) { return; }
			if (!goid$.go.HasCommon()) { return; }
			if (!goid$.go.HasAspect()) { return; }

			// set rollover name
			UIWindow wnd$ = UIShell.FindUIWindow("text", owner.InterfaceParent);
			if (wnd$ != null)
			{
				UIText txt$ = QueryDerivedText(wnd$);
				if (txt$ != null)
				{
					string s1$;

					goid$.go.common.GetScreenName(s1$);
					txt$.SetText(s1$, true);
				}
			}

			// adjust life bar
			wnd$ = UIShell.FindUIWindow("bar", owner.InterfaceParent);
			if (wnd$ != null)
			{
				float percent$ = goid$.go.Aspect.CurrentLife / goid$.go.Aspect.MaxLife;
				if (percent$ > 1.0) { percent$ = 1.0; }

				wnd$.SetRect(owner.left+0,owner.left+Math.Round(256*percent$),owner.top+0,owner.top+32,true);
				wnd$.SetUVRect(0.0, percent$, 0.0, 0.5);
			}

			UIShell.ShowInterface(owner.InterfaceParent);
		}
		hide$
		{
			UIShell.HideInterface(owner.InterfaceParent);
		}

		init$
		{
			hide$();
		}
	]];


	[t:dialog_box,n:background]
	{
		draw_order = 10;
		rect = 0,0,256,32;
		top_anchor = 50;
		is_top_anchor = true;
		texture = b_gui_ally_rollover_enemy;
		uvcoords = 0.0, 0.50, 1.0, 1.0;

		[messages]
		{
			oncreated = call(init$);
			onitemactivate = call(show$);
			onitemdeactivate = call(hide$);
		}
	}
	[t:window,n:bar]
	{
		draw_order = 11;
		rect = 0,0,256,32;
//		top_anchor=50;
//		is_top_anchor = true;
		texture = b_gui_ally_rollover_enemy;
		uvcoords = 0.0, 0.0, 1.0, 0.50;
	}
	[t:text,n:text]
	{
		draw_order = 12;
		rect = 0,0,256,16;
		top_anchor = 58;
		is_top_anchor = true;
		font_color = -1;
		font_size = 16;
		font_type = b_gui_fnt_16p_copperplate-light;
		justify = center;
		text = "OK";
		type = text;
	}
}